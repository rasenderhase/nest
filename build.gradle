// *** alte Schreibweise für Gradle-Plugins ***
buildscript {
	repositories {
		if (project.hasProperty("maven_host")) {
			maven {
				url maven_host
				credentials {
					username maven_username
					password maven_password
				}
			}
		} else {
			maven {
      			url "https://plugins.gradle.org/m2/"
    		}
		}
	}
	dependencies {
		classpath "com.bmuschko:gradle-nexus-plugin:2.3.1"
	}
}
apply plugin: "com.bmuschko.nexus"

// *** neue Schreibweise für Gradle-Plugins ***
//plugins {
	//id "com.bmuschko:nexus" version "2.3.1"
//}

apply plugin: "java"
apply plugin: "maven"
apply plugin: "eclipse"
apply plugin: "signing"
apply plugin: "jacoco"

group = theGroup
version = theVersion
sourceCompatibility = theSourceCompatibility

eclipse {
	classpath {
		containers.remove("org.eclipse.jdt.launching.JRE_CONTAINER")
		containers "org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8"
	}
}

jar {
	manifest {
		attributes "Implementation-Title": "nest NikEm STack",
		"Implementation-Version": version
	}
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

jacoco {
    toolVersion = "0.7.4+"	//see https://www.mkyong.com/gradle/gradle-jacoco-incompatible-version-1006/
}
jacocoTestReport {
    reports {
        xml.enabled true
    }
}
check.dependsOn jacocoTestReport

repositories {
	if (project.hasProperty("maven_host")) {
		maven {
			url maven_host
			credentials {
				username maven_username
				password maven_password
			}
		}
	} else {
		mavenCentral()
	}
}

dependencies {
	compile 'javax.servlet:javax.servlet-api:4.0.0-b01'
	compile "javax.servlet:jstl:1.2"
	
	compile "javax.ws.rs:javax.ws.rs-api:2.0.1"
	
	//@Inject annotation (used by de.nikem.nest.util.Messages)
	compile "javax.inject:javax.inject:1"
	
	//Viewable (used by de.nikem.nest.web.layout.ViewableFactory)
	compile("org.glassfish.jersey.ext:jersey-mvc-jsp:2.23.2") {
		exclude group: "javax.servlet", module: "servlet-api"
	}
	testCompile "junit:junit:4.12"
	testCompile "org.mockito:mockito-core:1.10.19"
}

test {
	systemProperties "property": "value"
}

[compileJava,compileTestJava]*.options*.encoding = "UTF-8"

eclipseJdt << {
	File f = file(".settings/org.eclipse.core.resources.prefs")
	f.write("eclipse.preferences.version=1\n")
	f.append("encoding/<project>=UTF-8")
}

modifyPom {
	project {
		name = "nest"
		description = "nest NikEm STack core library"
		url "https://github.com/rasenderhase/nest"

		licenses {
			license {
				name = "The Apache Software License, Version 2.0"
				url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
				distribution = "repo"
			}
		}
		developers {
			developer {
				name = "Andreas Knees"
				email = "andreas.knees@nikem.de"
				organization = "nikem GmbH"
				organizationUrl = "http://www.nikem.de"
			}
		}
		scm {
			connection "scm:git:git@github.com/rasenderhase/nest.git"
			developerConnection "scm:git:git@github.com/rasenderhase/nest.git"
			url "scm:git:git@github.com/rasenderhase/nest.git"
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.0"
}